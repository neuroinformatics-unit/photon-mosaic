configfile: "workflow/config.yaml"

from pathlib import Path
from photon_mosaic.dataset_discovery import discover_datasets
from photon_mosaic.rules.preprocessing import get_input_files
import logging

logging.basicConfig(level=logging.DEBUG)
logger = logging.getLogger("snakemake.workflow")

raw_data_base = Path(config["raw_data_base"])
processed_data_base = Path(config["processed_data_base"])
slurm_config = config.get("slurm", {})

logger.debug(f"Raw data base: {raw_data_base}")
logger.debug(f"Processed data base: {processed_data_base}")

# Discover datasets and their TIFF files
datasets_old_names, datasets_new_names, tiff_files = discover_datasets(
    str(raw_data_base),
    pattern=config["dataset_discovery"]["pattern"],
    exclude_patterns=config["dataset_discovery"].get("exclude_patterns"),
    substitutions=config["dataset_discovery"].get("substitutions"),
)

logger.debug(f"Discovered datasets (old names): {datasets_old_names}")
logger.debug(f"Discovered datasets (new names): {datasets_new_names}")
logger.debug(f"TIFF files: {tiff_files}")

# Create a mapping from dataset index to TIFF files
tiff_files_map = {i: tiff_files[dataset] for i, dataset in enumerate(datasets_old_names)}

preproc_targets = [
    str(
        Path(processed_data_base)
        / f"sub-{i}_{datasets_new_names[i]}"
        / f"ses-{j}"
        / "funcimg"
        / f"{tiff_name}"
    )
    for i in range(len(datasets_new_names))
    for j, tiff_name in enumerate(tiff_files_map[i])
]

logger.debug(f"Preprocessing targets: {preproc_targets}")

suite2p_targets = [
    str(
        Path(processed_data_base)
        / f"sub-{i}_{datasets_new_names[i]}"
        / f"ses-{j}"
        / "funcimg"
        / "suite2p"
        / "plane0"
        / fname
    )
    for i in range(len(datasets_new_names))
    for j in range(len(tiff_files_map[i]))
    for fname in ["F.npy", "data.bin"]
]

logger.debug(f"Suite2p targets: {suite2p_targets}")

# Flatten all tiff names for regex trick in preprocessing.smk
tiff_names_flat = sorted(set(tiff for tiffs in tiff_files.values() for tiff in tiffs))

TIFF_NAMES = tiff_names_flat

include: "preprocessing.smk"
include: "suite2p.smk"

rule all:
    input:
        preproc_targets,
        suite2p_targets
